include ~/toolsets/tulip/tulip-v7.hsl
include ~/toolsets/tulip/get_tlp_deps.hsl
include ~/fsutil.hsl

local var sources type list;
var deps type string;

project tabs : toolset "tulip-v7" : dependencies $deps : $sources;

tabs.prologue() {
    var option type list;

    $option = hefesto.sys.get_option("add-tab");

    if ($option.count() > 0) {
        hefesto.project.abort(add_tabs($option));
    }

    $option = hefesto.sys.get_option("get-tab");

    if ($option.count() > 0) {
        hefesto.project.abort(get_tabs($option));
    }

    $option = hefesto.sys.get_option("gen-tab");

    if ($option.count() > 0) {
        hefesto.project.abort(gen_tabs($option));
    }

    $sources = hefesto.sys.get_option("tlp");

    if ($sources.count() == 0) {
        $sources.ls(".*\\.tlp$");
        $deps = get_tlp_deps($sources);
    }

    $option = hefesto.sys.get_option("rebase");

    if ($option.count() > 0) {
        if (rebase() != 0) {
            hefesto.sys.echo("ERROR: Rebase was incomplete.\n");
            hefesto.project.abort(1);
        } else {
            hefesto.project.abort(0);
        }
    }
}

local function rebase() : result type int {
    var tlp type list;
    var t_nr type int;
    var t type int;

    $tlp.ls(".*\\.tlp$");
    $t_nr = $tlp.count();

    while ($t < $t_nr) {
        var filename type string;
        $filename = $tlp.item(0);
        $tlp.add_item(filenamefrompath($filename));
        $tlp.del_index(0);
        $t = $t + 1;
    }

    result add_tabs($tlp);
}

local function blackcat_flist(filenames type list) : result type string {
    var flist type string;
    var f type int;

    $f = 0;
    while ($f < $filenames.count()) {
        var filename type string;
        $filename = $filenames.item($f);
        $flist = $flist + main_tab_file($filename) + " part/" + part_pattern($filename) + " ";
        $f = $f + 1;
    }

    result $flist;
}

local function gen_tabs(filenames type list) : result type int {
    var exit_code type int;
    var ext type string;

    if (get_out_format($ext) == 0) {
        result 1;
    }

    $exit_code = do_blackcat_unlock($filenames);

    if ($exit_code == 0) {
        $exit_code = do_gen_tabs($filenames, $ext);
        if (do_git_checkout($filenames) != 0) {
            hefesto.sys.echo("!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!\n" +
                             "WARN: Git checkout has failed. Try to redo it on your own before pushing anything!\n" +
                             "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!\n");
        }
    }

    result $exit_code;
}

local function add_tabs(filenames type list) : result type int {
    var f type int;
    var file_list type string;

    $f = 0;
    while ($f < $filenames.count()) {
        var filename type string;
        $filename = $filenames.item($f);
        $file_list = $file_list + main_tab_file($filename) + " part/" + part_pattern($filename) + " ";
        $f = $f + 1;
    }

    result do_add_tab($file_list);
}

local function do_add_tab(file_list type string) : result type int {
    var errno type int;
    var rebase type list;

    $errno = hefesto.sys.run("blackcat add " + $file_list + " --lock");

    if ($errno != 0) {
        result $errno;
    }

    $rebase = hefesto.sys.get_option("rebase");

    if ($rebase.count() == 0) {
        $errno = hefesto.sys.run("git add " + $file_list);
    }

    result $errno;
}

local function do_blackcat_unlock(filenames type list) : result type int {
    result hefesto.sys.run("blackcat unlock " + blackcat_flist($filenames));
}

local function do_git_checkout(filenames type list) : result type int {
    result hefesto.sys.run("git checkout " + blackcat_flist($filenames) + "../.bcrepo/CATALOG");
}

local function do_gen_tabs(mainfiles type list, ext type string) : result type int {
    var exit_code type int;
    var m type int;

    $exit_code = 0;
    $m = 0;

    if (isdir($ext) == 0) {
        if (hefesto.sys.mkdir($ext) != 0) {
            hefesto.sys.echo("ERROR: Unable to create output directory '" + $ext + "'. Aborted.\n");
            result 1;
        }
    }

    hefesto.sys.echo("\n");

    while ($exit_code == 0 && $m < $mainfiles.count()) {
        var mainfile type string;
        $mainfile = $mainfiles.item($m);

        var outfile type string;
        $outfile = out_tab_file($mainfile, $ext);

        var additional_option type string;

        if ($ext == "html") {
            $additional_option = " --svg --svg-embed-font";
        } else if ($ext == "svg") {
            $additional_option = "--svg-embed-font";
        }

        $exit_code = hefesto.sys.run("tulip --tlp=" + main_tab_file($mainfile) + " --out=" + $outfile + $additional_option);

        if ($exit_code == 0) {
            hefesto.sys.echo("INFO: TAB of '" + $mainfile + "' generated at '" + $outfile + "'.\n");
        } else {
            hefesto.sys.echo("ERROR: Unable to generate TAB for '" + $mainfile + "'. Aborted.\n");
        }

        $m = $m + 1;
    }

    result $exit_code;
}

local function get_out_format(ext type string) : result type int {
    var supp_exts type list;
    var no_error type int;

    $no_error = 0;

    $supp_exts.add_item("pdf");
    $supp_exts.add_item("ps");
    $supp_exts.add_item("svg");
    $supp_exts.add_item("txt");
    $supp_exts.add_item("html");

    var option type list;
    $option = hefesto.sys.get_option("out-format");

    if ($option.count() == 0) {
        $ext = "pdf";
        $no_error = 1;
    } else {
        $ext = $option.item(0);
        if ($supp_exts.index_of($ext) == -1) {
            hefesto.sys.echo("ERROR: Unknown output format : '" + $ext + "'.\n");
            $ext = "";
        } else {
            $no_error = 1;
        }
    }

    hefesto.sys.byref($ext);
    result $no_error;
}

local function out_tab_file(filename type string, ext type string) : result type string {
    if ($filename.match("\\.tlp$") == 1) {
        $filename.replace("\\.tlp$", "");
    }
    result hefesto.sys.make_path($ext, $filename + "." + $ext);
}

local function main_tab_file(filename type string) : result type string {
    if ($filename.match("\\.tlp$") == 0) {
        $filename = $filename + ".tlp";
    }
    result $filename;
}

local function part_pattern(filename type string) : result type string {
    if ($filename.match(".*tlp$") == 1) {
        $filename.replace("\\.tlp$", "");
    }
    result $filename + "-*.tlp";
}

local function get_tabs(main_tab_files type list) : result type int {
    var current_tab_files type list;
    var m type int;
    var err type int;
    var main_tab_path type string;
    var files2unlock type string;

    $err = 1;

    $m = 0;
    while ($m < $main_tab_files.count()) {
        $main_tab_path = $main_tab_files.item($m);
        if (isfile($main_tab_path)) {
            $files2unlock = $files2unlock + $main_tab_path + " " + hefesto.sys.make_path("part", part_pattern($main_tab_path)) + " ";
        }
        $m = $m + 1;
    }

    $err = hefesto.sys.run("blackcat unlock " + $files2unlock);

    if ($err != 0) {
        result $err;
    }

    $err = 0;

    $m = 0;
    while ($m < $main_tab_files.count()) {
        $main_tab_path = $main_tab_files.item($m);
        if (isfile($main_tab_path)) {
            $err = $err + zip_tab($main_tab_path);
        }
        $m = $m + 1;
    }

    result $err;
}

local function zip_tab(main_tab_path type string) : result type int {
    var files2zip type list;

    $files2zip.add_item($main_tab_path);

    if (hefesto.sys.cd("part")) {
        var parts_pattern type string;
        $parts_pattern = $main_tab_path;
        $parts_pattern.replace("\\.tlp$", "-.*\\.tlp$");
        $files2zip.ls($parts_pattern);
        hefesto.sys.cd("..");
    }

    var out_zip_path type string;
    $out_zip_path = $main_tab_path;
    $out_zip_path.replace("\\.tlp$", ".zip");

    var out_dir type list;
    $out_dir = hefesto.sys.get_option("out-dir");
    if ($out_dir.count() > 0) {
        $out_zip_path = hefesto.sys.make_path($out_dir.item(0), $out_zip_path);
    }

    result zip($files2zip, $out_zip_path);
}

local function zip(files type list, out_path type string) : result type int {
    var err type int;
    $err = 1;
    if (has_7z()) {
        var cmdfiles type string;
        var f type int;
        $f = 0;
        while ($f < $files.count()) {
            var curr_file type string;
            $curr_file = $files.item($f);
            if ($curr_file.match(".*[/\\\\]part[/\\\\]") > 0) {
                $curr_file.replace("^.*[/\\\\]part[/\\\\]", "part/");
            }
            $cmdfiles = $cmdfiles + " " + $curr_file;
            $f = $f + 1;
        }
        $err = hefesto.sys.run("7z a " + $out_path + $cmdfiles + " >nul 2>&1");
    } else if (has_zip()) {
    }
    result $err;
}

local function has_zip() : result type int {
    # TODO(Rafael): Give support for zip.
    result 0;
}

local function has_7z() : result type int {
    if (hefesto.sys.os_name() != "windows") {
        result 0;
    }
    result (hefesto.sys.run("7z >nul 2>&1") == 0);
}
